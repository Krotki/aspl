import aspl.parser.ast
import aspl.parser.utils

[public]
class ListIndexExpression extends Expression {

    [readpublic]
    property Expression base
    [readpublic]
    property Expression index

    [public]
    method construct(Expression base, Expression index, Location? location){
        parent(Node).construct(location)
        this.base = base
        this.index = index
    }

    [public]
    method getType() returns Types{
        var list<Type> types = []
        foreach(base.getType().types as type){
            if(Type:getGenericTypes(type.toString()).length > 0){
                foreach(Type:getGenericTypes(type.toString())[0].types as t){
                    types.add(t)
                }
            }
        }
        return new Types(types)
    }

    [public]
    method isConstant() returns bool{
        return this.base.isConstant() && this.index.isConstant()
    }

    [public]
    method getConstantValue() returns any{
        return list<any>(this.base.getConstantValue())[int(this.index.getConstantValue())]
    }

}